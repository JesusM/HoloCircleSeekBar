// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

ext {
    versionMajor = 2
    versionMinor = 2
    versionPatch = 3

    versionCode = versionMajor * 1000 + versionMinor * 100 + versionPatch
    versionName = versionMajor + "." + versionMinor + "." + versionPatch

    minSdkVersion = 24
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = BUILD_TOOLS_VERSION
}

ext.libraries = [
        butterknife    : 'com.jakewharton:butterknife:7.0.1',
        appcompat      : 'com.android.support:appcompat-v7:' + SUPPORT_LIBS_VERSION,
        supportv4      : 'com.android.support:support-v4:' + SUPPORT_LIBS_VERSION,
        supportv7      : 'com.android.support:support-v7:' + SUPPORT_LIBS_VERSION,
        supportv13     : 'com.android.support:support-v13:' + SUPPORT_LIBS_VERSION,
]

wrapper {
    gradleVersion = '4.4'
    distributionUrl = distributionUrl.replace("bin", "all")
}


project.ext.preDexLibs = project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}
